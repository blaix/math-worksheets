port module Components.Equations exposing (component, Model, Msg, Props)

import Prettynice.Component exposing (Component)
import Transmutable.Html as H exposing (Html)
import Transmutable.Html.Attributes as A
import Transmutable.Html.Events as E
import Random


component : Component Props Model Msg
component =
    { init = init
    , update = update
    , subscriptions = subscriptions
    , view = view
    }


-- MODEL


type alias Props =
    { seed : Int
    , count : Int
    }


type alias Model =
    Props


type WhereVar
    = First
    | Second


type AnswerSide
    = Left
    | Right


type Operation
    = Add
    | Subtract
    | Multiply


{-| Possible values for variable names.
-}
varNames : Array String
varNames =
    "abcdefghijkmnpqrstuvwxyz"
        |> String.split ""


init : Props -> { model : Model, command : Cmd Msg }
init props =
    { model = props
    , command = Cmd.none
    }


-- UPDATE


type Msg
    = Refresh
    | More
    | Less
    | Print


update : Msg -> Model -> { model : Model, command : Cmd Msg }
update msg model =
    case msg of
        Refresh ->
            { model = { model | seed = model.seed + model.count + 1 }
            , command = Cmd.none
            }

        More ->
            { model = { model | count = model.count + 1 }
            , command = Cmd.none
            }

        Less ->
            let
                newCount =
                    if model.count < 1 then
                        0
                    else
                        model.count - 1
            in
            { model = { model | count = newCount }
            , command = Cmd.none
            }

        Print ->
            { model = model
            , command = print {}
            }


port print : {} -> Cmd msg


subscriptions : Model -> Sub Msg
subscriptions _ =
    Sub.none


-- VIEW


view : Model -> Html Msg
view model =
    H.div []
        [ H.div [ A.class "no-print" ] 
            [ H.button [ E.onClick Refresh ] [ H.text "Refresh" ]
            , H.button [ E.onClick More ] [ H.text "More" ]
            , H.button [ E.onClick Less ] [ H.text "Less" ]
            , H.button [ E.onClick Print ] [ H.text "Print" ]
            ]
        , Array.range 0 model.count 
            |> Array.map
                (\i ->
                    H.p [] [ viewRandomEquation (model.seed + i) ]
                )
            |> H.div []
        ]


viewRandomEquation : Int -> Html Msg
viewRandomEquation seed =
    let
        seed0 =
            Random.initialSeed seed

        { value = value1, seed = seed1 } = 
            Random.step (Random.int 2 9) seed0

        { value = value2, seed = seed2 } = 
            Random.step (Random.int 2 9) seed1

        { value = whereVar, seed = seed3 } =
            Random.step (Random.uniform First [ Second ]) seed2

        { value = answerSide, seed = seed4 } =
            Random.step (Random.uniform Left [ Right ]) seed3

        { value = operation, seed = seed5 } =
            Random.step (Random.uniform Add [ Subtract, Multiply ]) seed4

        { value = varName } =
            Random.step
                ( Random.int 0 ((Array.length varNames) - 1)
                    |> Random.map
                        ( \i -> 
                            Array.get i varNames
                                |> Maybe.withDefault "x"
                        )
                )
                seed2

        values =
            if value1 > value2 then
                { left = value1
                , right = value2
                }

            else
                { left = value2
                , right = value1
                }

        answer =
            String.fromInt <|
                case operation of
                    Add -> 
                        values.left + values.right       

                    Subtract -> 
                        values.left - values.right

                    Multiply ->
                        values.left * values.right

        operationSymbol =
            case operation of
                Add -> 
                    " + "

                Subtract -> 
                    " - "

                Multiply -> 
                    case whereVar of
                        First ->
                            " â€¢ "

                        Second ->
                            ""
        
        expression =
            case whereVar of
                First ->
                    varName ++ operationSymbol ++ (String.fromInt values.right)
            
                Second ->
                    (String.fromInt values.left) ++ operationSymbol ++ varName

        equation =
            case answerSide of
                Left ->
                    answer ++ " = " ++ expression

                Right ->
                    expression ++ " = " ++ answer
    in
    H.text equation


